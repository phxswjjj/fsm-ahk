#Include ./Utility/Common.ahk2
#Include ./Data/FightingSettings.ahk2
#Include ./Action/WalkHorizontally.ahk2

AppExe := "FSM-Win64-Shipping.exe"
UniqueID := WinExist("ahk_exe " AppExe)
if not UniqueID {
    OutputLog("[" AppExe "] not found")
    ExitApp
}
OutputLog("[" AppExe "] found [" UniqueID "]")

FightingSetting := FightingSettings()
FightingSetting.AutoFighting := false

WalkCtrl := WalkHorizontally()

SetTimer OuterWalkingJob, 200
SetTimer FightingJob, 200
SetTimer FightLoseJob, 200

; 5 hours
SetTimer CollectStoneJob, 5 * 60 * 60 * 1000

Return

OuterWalkingJob() {
    If not WinActive("ahk_id " UniqueID)
        return

    ; 左右移動
    If (PixelGetColor(76, 984, 'RGB') == 0xDFECE6) {
        WalkCtrl.Walk(1000)
        Sleep(500)
    }
}

FightingJob() {
    If not WinActive("ahk_id " UniqueID)
        return

    ; 自動戰鬥
    If (FightingSetting.AutoFighting and PixelGetColor(92, 60, 'RGB') == 0xAABCB8) {
        OutputLog("Click 自動戰鬥")
        Click(92, 60)
        Sleep(10000)
    }
    ; 取消自動戰鬥
    If (not FightingSetting.AutoFighting and PixelGetColor(92, 60, 'RGB') == 0xDEF7F7) {
        OutputLog("Click 取消自動戰鬥")
        Click(92, 60)
        Sleep(500)
    }

    ; 施放技能
    If (not FightingSetting.AutoFighting 
        and PixelGetColor(92, 60, 'RGB') == 0xAABCB8
        and PixelGetColor(1833, 776, 'RGB') == 0xE7F3EF) {
        Send(FightingSetting.ManualSkillCode)
        Sleep(100)
        ; 戰術支援
        Send('f')
        Sleep(200)
        For pos in FightingSetting.MonsterPositions {
            Click(pos*)
            Sleep(100)
        }
        Sleep(1000)
    }
    
    ; 戰鬥勝利
    If (PixelGetColor(423, 151, 'RGB') == 0xFAF9A7) {
        OutputLog("Click 戰鬥勝利")
        Loop 10 {
            If (PixelGetColor(423, 151, 'RGB') == 0xFAF9A7) {
                ; click any
                Click(1041, 222)
                Sleep(500)
            } else
                break
        }
    }
}

FightLoseJob() {
    If not WinActive("ahk_id " UniqueID)
        return
    
    ; 戰敗
    If (PixelGetColor(971, 587, 'RGB') == 0xB461E6
        and PixelGetColor(758, 334, 'RGB') == 0xE9ADFD) {
        ; 按鈕緩慢出現
        Sleep(2500)
        OutputLog("Click 戰敗返回重生點")
        Click(1167, 853)
        Sleep(2000)
    }
}

CollectStoneJob() {
    If not WinActive("ahk_id " UniqueID)
        return

    If (PixelGetColor(791, 731, 'RGB') != 0xDEE4D7)
        return

    btnColor := PixelGetColor(1248, 900, 'RGB')
    If (btnColor == 0xFFF5CA or btnColor == 0xEEE4CD) {
        OutputLog("Click 五行源石一鍵領取")
        Click(1248, 900)
        Sleep(2000)
        ; 保存結果
        Send('{PrintScreen}')
        Sleep(1000)
        Click(886, 835)
        Sleep(1000)
    }
}

^!d:: ; debug
{
    color := PixelGetColor(1248, 900, 'RGB')
    OutputLog(color)
}

; 賭博失敗重開
$f1::
{
    global UniqueID

    Runwait("taskkill /im " AppExe " /f")
    Sleep(1000)
    OutputLog("Restart..")
    Run("steam://rungameid/1597460")

    ; reload
    loop 30
    {
        UniqueID := WinExist("ahk_exe " AppExe)
        if (not UniqueID)
            Sleep(1000)
    }
    if not UniqueID {
        OutputLog("[" AppExe "] not found")
        ExitApp
    }
    OutputLog("[" AppExe "] found [" UniqueID "]")
}

^!z:: ; Ctrl+Alt+Z hotkey.
{
    OutputLog("Z pressed")
    OutputLog("Stopped")
    ExitApp
}